{pkgs, myvars, config, ...}: {
  services = {
    greetd = { # https://wiki.archlinux.org/title/Greetd
      enable = true;
      settings = let
        my_sessions = pkgs.runCommand "greetd-sessions" {} ''
          mkdir -p $out

          # `.wayland-session` is a script generated by home-manager, which
          # links to the current wayland compositor (hyprland or others). With
          # such a vendor-no-locking script, we can switch to another wayland
          # compositor without modifying greetd's config here.
          cat <<EOF > $out/hm-session.desktop
          [Desktop Entry]
          Name=Home Manager Session
          Exec=sh -c "\$HOME/.wayland-session"
          Type=Application
          EOF

          cat <<EOF > $out/steam.desktop
          [Desktop Entry]
          Name=Steam (Gamescope)
          Exec=${config.programs.gamescope.package}/bin/gamescope -W 1920 -H 1080 -f -e --xwayland-count 2 --hdr-enabled --hdr-itm-enabled -- steam -pipewire-dmabuf -gamepadui -steamdeck -steamos3
          Type=Application
          EOF
        '';
      in {
        default_session = { # Wayland Desktop Manager is installed only for user
          user = myvars.username;
          command = "${pkgs.tuigreet}/bin/tuigreet --time --remember --remember-session --sessions ${my_sessions}";
        };
      };
    };
    gvfs.enable = true; # Mount, trash, and other functionalities
    tumbler.enable = true; # Thumbnail support for images
  };
  xdg.portal = {
    enable = true;
    wlr.enable = true;
    extraPortals = [pkgs.xdg-desktop-portal-gtk];
    config.common.default = ["gtk"]; # Use xdg-desktop-portal-gtk for every portal interface
  };
  ## START security.nix
  # Security with gnome-kering
  services.gnome.gnome-keyring.enable = true;
  security.pam.services.greetd.enableGnomeKeyring = true;
  # Secret portal is handled by gnome-keyring
  xdg.portal.config.common."org.freedesktop.impl.portal.Secret" = ["gnome-keyring"];
  ## END security.nix
  ## START misc.nix
  programs = {
    dconf.enable = true;
    thunar = { # thunar file manager
      enable = true;
      plugins = with pkgs.xfce; [thunar-archive-plugin thunar-volman];
    };
  };
  ## END misc.nix
}
