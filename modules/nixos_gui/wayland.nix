{lib, pkgs, myvars, ...}: {
  environment.systemPackages = [pkgs.wl-clipboard];
  services = {
    xserver.enable = lib.mkDefault false; # Disable xorg server
    greetd = { # https://wiki.archlinux.org/title/Greetd
      enable = lib.mkDefault true;
      settings = {
        default_session = { # Wayland Desktop Manager is installed only for user via home-manager
          # `.wayland-session` is a script generated by home-manager, which links to the current wayland compositor
          # (hyprland or others). With such a vendor-no-locking script, we can switch to another wayland compositor without modifying greetd's config here.
          # command = "$HOME/.wayland-session"; # Start a wayland session directly without a login manager
          # Start wayland session with a TUI login manager
          user = lib.mkOverride 999 myvars.username;
          command = lib.mkDefault "${pkgs.greetd.tuigreet}/bin/tuigreet --time --cmd $HOME/.wayland-session";
        };
      };
    };
    gvfs.enable = lib.mkDefault true; # Mount, trash, and other functionalities
    tumbler.enable = lib.mkDefault true; # Thumbnail support for images
  };
  xdg.portal = {
    enable = lib.mkDefault true;
    wlr.enable = lib.mkDefault true;
    extraPortals = with pkgs; [xdg-desktop-portal-gtk];
    config.common.default = ["gtk"]; # Use xdg-desktop-portal-gtk for every portal interface
  };
  ## START security.nix
  security.polkit.enable = lib.mkDefault true;

  # Security with gnome-kering
  services.gnome.gnome-keyring.enable = lib.mkDefault true;
  security.pam.services.greetd.enableGnomeKeyring = lib.mkDefault true;
  # Secret portal is handled by gnome-keyring
  xdg.portal.config.common."org.freedesktop.impl.portal.Secret" = ["gnome-keyring"];

  security.pam.services.swaylock = {}; # Fix https://github.com/ryan4yin/nix-config/issues/10
  ## END security.nix
  ## START misc.nix
  programs = {
    dconf.enable = lib.mkDefault true;
    light.enable = lib.mkDefault true;
    thunar = { # thunar file manager
      enable = lib.mkDefault true;
      plugins = with pkgs.xfce; [thunar-archive-plugin thunar-volman];
    };
  };
  ## END misc.nix
}
